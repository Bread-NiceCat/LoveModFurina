plugins {
    id 'java'
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "io.github.pacifistmc.forgix" version "1.2.+"
}
architectury {
    minecraft = minecraft_version
}
String archive_file_base_name = "${archives_name}-${minecraft_version}"

forgix {
    group = maven_group
    mergedJarName = "${archive_file_base_name}-merged-${mod_version}.jar"
    outputDir = "build/libs/merged"
    removeDuplicate "cn.breadnicecat.lovemod"
    neoforge {
        jarLocation = "build/libs/${archive_file_base_name}-neoforge-${mod_version}.jar"
        mixin "lovemod.mixin.json"
        return null
    }
    fabric {
        jarLocation = "build/libs/${archive_file_base_name}-fabric-${mod_version}.jar"
        return null
    }
}
build.finalizedBy(mergeJars)
allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = mod_version
    group = maven_group
    archivesBaseName = archive_file_base_name

    repositories {
        if (Locale.getDefault() == Locale.CHINA) {
            var aliyun = maven { url = "https://maven.aliyun.com/repository/public" }
            remove aliyun
            addFirst aliyun
        }
        maven { url = "https://modmaven.dev" }
        maven { url = 'https://maven.parchmentmc.org' }
    }
}
subprojects {
    apply plugin: "dev.architectury.loom"
    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:${parchment_version}")
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }
    }
}

//declare repositories
println("=".repeat(64))
println("here in project $project.name")
println("repo here=>")
getRepositories().forEach { it ->
    if (it instanceof MavenArtifactRepository) {
        println it.url
    } else if (it instanceof FlatDirectoryArtifactRepository) {
        println it.dirs
    } else print it
}
println("repo end")
println("=".repeat(64))
//================================================================//
